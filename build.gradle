plugins {
    id "idea"
    id "org.jetbrains.kotlin.kapt" version "1.9.22"
    id "org.jetbrains.kotlin.jvm" version "1.9.25"
    id "io.spring.dependency-management" version "1.1.0"
    id "org.jlleitschuh.gradle.ktlint" version "12.1.0" apply false
    id "org.jetbrains.kotlin.plugin.jpa" version "1.9.25" apply false
    id "org.jetbrains.kotlin.plugin.spring" version "1.9.25" apply false
    id "org.springframework.boot" version "3.2.8" apply false
    id "com.gorylenko.gradle-git-properties" version "2.4.1"
}

ext {
    springBootVersion = "3.2.8"
    springCloudVersion = "2023.0.3"
    openApiVersion = "2.6.0"
    bucket4jVersion = "7.6.1"
    postgresContainerVersion = "1.20.1"
}

allprojects {
    apply plugin: "idea"
    apply plugin: "kotlin"
    apply plugin: "org.jetbrains.kotlin.kapt"
    apply plugin: "org.jlleitschuh.gradle.ktlint"
    apply plugin: "io.spring.dependency-management"

    [compileKotlin, compileTestKotlin]*.kotlinOptions*.jvmTarget = "17"

    idea {
        module {
            downloadJavadoc = true
            downloadSources = true
        }
    }

    repositories {
        mavenCentral()
    }

    ktlint {
        version.set("1.2.1")
        filter {
            exclude { entry -> entry.file.toString().contains("generated") }
        }
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:$springBootVersion"
        }
    }

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-reflect"
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
        implementation "com.fasterxml.jackson.module:jackson-module-kotlin"
    }

    test {
        useJUnitPlatform()
        testLogging {
            showStandardStreams = true
        }
    }

    processResources {
        filesMatching(["**/properties-from-gradle.properties"]) {
            expand(project.properties)
        }
    }
}
